plugins {
    id 'kotlin-multiplatform'
    id 'com.android.library'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'mirego.release' version '1.13'
    id 'mirego.publish' version '1.0'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://jitpack.io' }
}

group 'com.mirego.trikot'

kotlin {
    android() {
        publishAllLibraryVariants()
    }
    jvm()
    ios()
    iosArm32('iosArm32')
    tvos()
    js()

    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.time.ExperimentalTime')
            }
        }

        commonMain {}

        jvmShared {
            dependsOn commonMain
            dependencies {
                implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
            }
        }

        jvmMain {
            dependsOn jvmShared
        }

        androidMain {
            dependsOn jvmShared
        }

        jsMain {}

        nativeMain {
            dependsOn commonMain
        }

        iosMain {
            dependsOn nativeMain
            iosX64Main.dependsOn(it)
            iosArm64Main.dependsOn(it)
        }

        tvosMain {
            dependsOn nativeMain
            tvosArm64Main.dependsOn(it)
            tvosX64Main.dependsOn(it)
        }

        iosArm32Main {
            dependsOn nativeMain
        }

        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }

        jvmTest {
            dependsOn commonTest
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        androidTest {
            dependsOn jvmTest
        }

        nativeTest {
            dependsOn commonTest
            iosX64Test.dependsOn(it)
            iosArm64Test.dependsOn(it)
            tvosArm64Test.dependsOn(it)
            tvosX64Test.dependsOn(it)
            iosArm32Test.dependsOn(it)
        }
    }
}

release {
    checkTasks = ['check']
    buildTasks = ['publish']
    updateVersionPart = 2
}
