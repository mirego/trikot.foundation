plugins {
    id 'com.android.library'
    id 'kotlin-multiplatform'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'mirego.release' version '2.0'
    id 'mirego.publish' version '1.0'
}

repositories {
    google()
    jcenter()
    mavenLocal()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://jitpack.io' }
}

group 'com.mirego.trikot'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
    }
}

kotlin {
    android {
        publishAllLibraryVariants()
    }
    jvm()
    ios()
    iosArm32('iosArm32')
    tvos()
    js {
        browser()
        nodejs()
    }

    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.time.ExperimentalTime')
            }
        }

        commonMain {}

        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }

        jvmShared {
            dependsOn commonMain
            dependencies {
                implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
            }
        }

        jvmMain {
            dependsOn jvmShared
        }

        jvmTest {
            dependsOn commonTest
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        androidMain {
            dependsOn jvmShared
        }

        androidTest {
            dependsOn jvmTest
        }

        jsMain {
            dependsOn(commonMain)
        }

        jsTest {
            dependsOn commonTest
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }

        nativeMain {
            dependsOn commonMain
        }

        nativeTest {
            dependsOn commonTest
            iosX64Test.dependsOn(it)
            iosArm64Test.dependsOn(it)
            tvosArm64Test.dependsOn(it)
            tvosX64Test.dependsOn(it)
            iosArm32Test.dependsOn(it)
        }

        iosMain {
            dependsOn nativeMain
            iosX64Main.dependsOn(it)
            iosArm64Main.dependsOn(it)
        }

        tvosMain {
            dependsOn nativeMain
            tvosArm64Main.dependsOn(it)
            tvosX64Main.dependsOn(it)
        }

        iosArm32Main {
            dependsOn nativeMain
        }
    }
}

release {
    checkTasks = ['check']
    buildTasks = ['publish']
    updateVersionPart = 2
}
