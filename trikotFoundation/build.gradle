plugins {
    id 'kotlin-multiplatform'
    id 'com.android.library'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'mirego.release' version '1.13'
    id 'mirego.publish' version '1.0'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://jitpack.io' }
}

group 'com.mirego.trikot'

kotlin {
    android {
        publishAllLibraryVariants()
    }

    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.android, 'android')
        fromPreset(presets.js, 'js')
        fromPreset(presets.iosArm32, 'iosArm32')
        fromPreset(presets.iosArm64, 'iosArm64')
        fromPreset(presets.iosX64, 'iosX64')
        fromPreset(presets.tvosArm64, 'tvosArm64')
        fromPreset(presets.tvosX64, 'tvosX64')
    }
    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.time.ExperimentalTime')
            }
        }

        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }

        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }

        jvmShared {
            dependsOn(commonMain)
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
            }
        }

        jvmMain {
            dependsOn(jvmShared)
        }

        androidMain {
            dependsOn(jvmShared)
        }

        androidTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }

        nativeMain {
            dependsOn(commonMain)
        }

        nativeMain32 {
            dependsOn(nativeMain)
        }

        nativeMain64 {
            dependsOn(nativeMain)
        }

        configure([iosX64Main, iosArm64Main, tvosArm64Main, tvosX64Main]) {
            dependsOn nativeMain64
        }

        iosArm32Main {
            dependsOn(nativeMain32)
        }
    }
}

release {
   checkTasks = ['check']
   buildTasks = ['publish']
   updateVersionPart = 1
}
