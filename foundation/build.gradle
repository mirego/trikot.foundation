plugins {
    id 'kotlin-multiplatform'
    id 'groovy'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'mirego.release' version '1.13'
    id 'mirego.publish' version '1.0'
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://jitpack.io' }
}

group 'com.mirego.trikot'

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'js')
        fromPreset(presets.iosArm32, 'iosArm32')
        fromPreset(presets.iosArm64, 'iosArm64')
        fromPreset(presets.iosX64, 'iosX64')
        fromPreset(presets.tvosArm64, 'tvosArm64')
        fromPreset(presets.tvosX64, 'tvosX64')
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }

        nativeMain {
            dependsOn(commonMain)
        }

        nativeMain32 {
            dependsOn(nativeMain)
        }

        nativeMain64 {
            dependsOn(nativeMain)
        }

        iosArm32Main {
            dependsOn(nativeMain32)
        }

        iosArm64Main {
            dependsOn(nativeMain64)
        }

        iosX64Main {
            dependsOn(nativeMain64)
        }

        tvosArm64Main {
            dependsOn(nativeMain64)
        }

        tvosX64Main {
            dependsOn(nativeMain64)
        }
    }
}

release {
   checkTasks = ['check']
   buildTasks = ['publish']
   updateVersionPart = 1
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}
